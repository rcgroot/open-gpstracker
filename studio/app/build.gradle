apply plugin: 'com.android.application'

project.archivesBaseName = "opengpstracker"

android {
    compileSdkVersion "Google Inc.:Google APIs:23"
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "nl.sogeti.android.gpstracker"
        testApplicationId "nl.sogeti.android.gpstracker.tests"
        minSdkVersion 7
        targetSdkVersion 14
        versionCode 1500
        versionName "1.5.0"
        buildConfigField "int", "BUILD_NUMBER", System.getenv("BUILD_NUMBER") as String ?: "0"
        buildConfigField "String", "GIT_COMMIT", "\"" + System.getenv("GIT_COMMIT") + "\"" as String ?: "\"Unknown\""
        generatedDensities = ["hdpi", "xxhdpi"]
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:preference-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile project(':log')
    compile project(':service')
    testCompile 'junit:junit:4.12'
}

def keystorePropertiesFile = rootProject.file("keystore.properties");
if (keystorePropertiesFile.exists()) {
    def signingProperties = new Properties()
    signingProperties.load(new FileInputStream(keystorePropertiesFile))
    def release = android.signingConfigs.release
    release.storeFile = file(signingProperties["storeFile"])
    release.storePassword = signingProperties["storePassword"]
    release.keyAlias = signingProperties["keyAlias"]
    release.keyPassword = signingProperties["keyPassword"]
}
